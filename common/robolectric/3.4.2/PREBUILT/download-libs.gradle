/*
 * Modified from https://gist.github.com/xian/05c4f27da6d4156b9827842217c2cd5c
 * Reference: http://robolectric.org/blog/2017/03/01/hermetic-builds/
 */

defaultTasks 'copyLibs'

def shadowArtifacts = [
        "org.robolectric:framework:${robolectricVersion}",
        "org.robolectric:httpclient:${robolectricVersion}",
        "org.robolectric:maps:${robolectricVersion}",
        "org.robolectric:multidex:${robolectricVersion}",
        "org.robolectric:playservices:${robolectricVersion}",
        "org.robolectric:supportv4:${robolectricVersion}",
]

def buildDir = System.getProperty("user.dir")

apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    sandbox
}


// In this section you declare the dependencies for your production and test code
dependencies {
    compile("org.robolectric:robolectric:${robolectricVersion}") {
        // we don't need these MavenDependencyResolver in a hermetic build
        exclude group: 'org.apache.maven', module: 'maven-ant-tasks'
        exclude group: 'org.apache.ant', module: 'ant'
    }

    compile('com.ximpleware:vtd-xml:2.11')

    shadowArtifacts.forEach { shadowArtifact ->
        compile shadowArtifact
        sandbox shadowArtifact
    }
}


task copyLibs(type: Copy) {
    into "$buildDir/lib"
    from configurations.compile

    doLast {
        def f = new File("$buildDir/classpath_jars.mk")
        f.delete()

        f << "my_robolectric_runtime_deps := \\\n"

        source.forEach { file ->
            f << "    lib/${file.name} \\\n"
        }
        f << "\n"
    }
}
